plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
}

apply from: "$rootDir/dependencies.gradle"
apply from: "$rootDir/config.gradle"

android {
    defaultConfig {
        compileSdkVersion config.compileSdk
        minSdkVersion config.minSdk
        targetSdkVersion config.targetSdk
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    flavorDimensions "dim"
    productFlavors {
        'native' {
            versionNameSuffix "-native"
            dimension = "dim"
        }

        react {
            versionNameSuffix "-react"
            dimension = "dim"
        }
    }
}

dependencies {
    implementation "androidx.annotation:annotation:1.3.0"

    nativeImplementation (deps['facebook-flipper'])
    reactImplementation (deps['facebook-flipper']) {
        exclude group:'com.facebook.fbjni'
    }
    nativeImplementation (deps['flipper-network-plugin'])
    reactImplementation (deps['flipper-network-plugin']) {
        exclude group:'com.facebook.flipper'
    }

    implementation deps['flipper-mmkv-plugin']
    compileOnly deps['okhttp']

    nativeImplementation deps['soloader']
    reactCompileOnly deps['soloader']
}

task androidJavadocs(type: Javadoc){
    source = android.sourceSets.main.java.srcDirs

    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    android.libraryVariants.all{ variant->
        if (variant.name == 'release'){
            owner.classpath += variant.javaCompileProvider.get().classpath
        }
    }

    title = null
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs){
    archiveClassifier.set('javadoc')
    from androidJavadocs.destinationDir
}


task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

afterEvaluate {
    publishing {
        publications {
            reactRelease(MavenPublication){
                groupId = 'com.piex.library'
                artifactId = 'flipper-util-react'
                version = flipperUtilVersion

                from components.reactRelease
                artifact androidSourcesJar
                artifact androidJavadocsJar
            }

            nativeRelease(MavenPublication){
                groupId = 'com.piex.library'
                artifactId = 'flipper-util-native'
                version = flipperUtilVersion

                from components.nativeRelease
                artifact androidSourcesJar
                artifact androidJavadocsJar
            }
        }
    }
}
